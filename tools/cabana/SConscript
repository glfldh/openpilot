import os

Import('env', 'arch', 'common', 'messaging', 'visionipc', 'cereal')
import qt5

SConscript(['#tools/replay/SConscript'])
Import('replay_lib')

qt_env = env.Clone()
qt_modules = ["Widgets", "Gui", "Core", "Network", "Concurrent", "Xml"]

# Use vendored Qt
qt_env['QTDIR'] = qt5.DIR
qt_env['QT3DIR'] = qt5.DIR
qt_env.Tool('qt3')

qt_dirs = [qt5.INCLUDE_DIR]
qt_dirs += [f"{qt5.INCLUDE_DIR}/Qt{m}" for m in qt_modules]

# QtGui private headers (for QOpenGLWidget internals)
qt_gui_path = os.path.join(qt5.INCLUDE_DIR, "QtGui")
if os.path.isdir(qt_gui_path):
  qt_gui_dirs = [d for d in os.listdir(qt_gui_path) if os.path.isdir(os.path.join(qt_gui_path, d))]
  if qt_gui_dirs:
    qt_dirs.append(f"{qt5.INCLUDE_DIR}/QtGui/{qt_gui_dirs[0]}/QtGui")

qt_env['CPPPATH'] += qt_dirs + ["#third_party/qrcode"]
qt_env['LIBPATH'] += [qt5.LIB_DIR]
qt_env['RPATH'] += [qt5.LIB_DIR]
qt_env.AppendENVPath('PATH', qt5.BIN_DIR)

qt_flags = [
  "-D_REENTRANT",
  "-DQT_NO_DEBUG",
  "-DQT_WIDGETS_LIB",
  "-DQT_GUI_LIB",
  "-DQT_CORE_LIB",
  "-DQT_MESSAGELOGCONTEXT",
]
qt_env['CXXFLAGS'] += qt_flags

# Shared library linking (both platforms)
qt_libs = [f"Qt5{m}" for m in qt_modules]
qt_libs += ["Qt5Charts", "Qt5SerialBus"]

if arch == "Darwin":
  qt_env["FRAMEWORKS"] += ["OpenGL"]
else:
  qt_libs += ["GL"]

qt_env['LIBS'] = qt_libs

base_frameworks = qt_env['FRAMEWORKS']
base_libs = [common, messaging, cereal, visionipc, 'm', 'ssl', 'crypto', 'pthread'] + qt_env["LIBS"]

qt_libs = base_libs

cabana_env = qt_env.Clone()

cabana_libs = [cereal, messaging, visionipc, replay_lib, 'avformat', 'avcodec', 'swresample', 'avutil', 'x264', 'z', 'zstd', 'curl', 'yuv', 'usb-1.0'] + qt_libs
opendbc_path = '-DOPENDBC_FILE_PATH=\'"%s"\'' % (cabana_env.Dir("../../opendbc/dbc").abspath)
cabana_env['CXXFLAGS'] += [opendbc_path]
cabana_env['CXXFLAGS'] += [f'-DVENDORED_QT_PLUGIN_PATH=\'"{qt5.PLUGINS_DIR}"\'']

# build assets
assets = "assets/assets.cc"
assets_src = "assets/assets.qrc"
cabana_env.Command(assets, assets_src, f"rcc $SOURCES -o $TARGET")
cabana_env.Depends(assets, Glob('/assets/*', exclude=[assets, assets_src, "assets/assets.o"]))

cabana_lib = cabana_env.Library("cabana_lib", ['mainwin.cc', 'streams/socketcanstream.cc', 'streams/pandastream.cc', 'streams/devicestream.cc', 'streams/livestream.cc', 'streams/abstractstream.cc', 'streams/replaystream.cc', 'binaryview.cc', 'historylog.cc', 'videowidget.cc', 'signalview.cc',
                                               'streams/routes.cc', 'dbc/dbc.cc', 'dbc/dbcfile.cc', 'dbc/dbcmanager.cc',
                                               'utils/export.cc', 'utils/util.cc', 'utils/elidedlabel.cc', 'utils/api.cc',
                                               'chart/chartswidget.cc', 'chart/chart.cc', 'chart/signalselector.cc', 'chart/tiplabel.cc', 'chart/sparkline.cc',
                                               'commands.cc', 'messageswidget.cc', 'streamselector.cc', 'settings.cc', 'panda.cc',
                                               'cameraview.cc', 'detailwidget.cc', 'tools/findsimilarbits.cc', 'tools/findsignal.cc', 'tools/routeinfo.cc'], LIBS=cabana_libs, FRAMEWORKS=base_frameworks)
cabana_env.Program('_cabana', ['cabana.cc', cabana_lib, assets], LIBS=cabana_libs, FRAMEWORKS=base_frameworks)

if GetOption('extras'):
  cabana_env.Program('tests/test_cabana', ['tests/test_runner.cc', 'tests/test_cabana.cc', cabana_lib], LIBS=[cabana_libs])

output_json_file = 'tools/cabana/dbc/car_fingerprint_to_dbc.json'
generate_dbc = cabana_env.Command('#' + output_json_file,
                                  ['dbc/generate_dbc_json.py'],
                                  "python3 tools/cabana/dbc/generate_dbc_json.py --out " + output_json_file)
cabana_env.Depends(generate_dbc, ["#common", '#opendbc_repo', "#cereal", "#msgq_repo"])
