import os
import glob

Import('env', 'arch')
lenv = env.Clone()

tinygrad_root = env.Dir("#").abspath
tinygrad_files = ["#"+x for x in glob.glob(env.Dir("#tinygrad_repo").relpath + "/**", recursive=True, root_dir=tinygrad_root)
                  if 'pycache' not in x and os.path.isfile(os.path.join(tinygrad_root, x))]

# Get model metadata
for model_name in ['driving_vision', 'driving_policy', 'dmonitoring_model']:
  fn = File(f"models/{model_name}").abspath
  script_files = [File(Dir("#selfdrive/modeld").File("get_model_metadata.py").abspath)]
  cmd = f'python3 {Dir("#selfdrive/modeld").abspath}/get_model_metadata.py {fn}.onnx'
  lenv.Command(fn + "_metadata.pkl", [fn + ".onnx"] + tinygrad_files + script_files, cmd)

# compile warp
# THREADS=0 is need to prevent bug: https://github.com/tinygrad/tinygrad/issues/14689
tg_flags = {
    'larch64': 'DEV=QCOM FLOAT16=1 NOLOCALS=1 JIT_BATCH_SIZE=0',
    'Darwin': f'DEV=CPU THREADS=0 HOME={os.path.expanduser("~")}', # tinygrad calls brew which needs a $HOME in the env
}.get(arch, 'DEV=CPU CPU_LLVM=1 THREADS=0')
image_flag = {
     'larch64': 'IMAGE=2',
}.get(arch, 'IMAGE=0')
script_files = [File(Dir("#selfdrive/modeld").File("compile_warp.py").abspath)]
cmd = f'{tg_flags} python3 {Dir("#selfdrive/modeld").abspath}/compile_warp.py '
from openpilot.selfdrive.modeld.compile_warp import CAMERA_CONFIGS
warp_targets = []
for cam in CAMERA_CONFIGS:
  w, h = cam.width, cam.height
  warp_targets += [File(f"models/warp_{w}x{h}_tinygrad.pkl").abspath, File(f"models/dm_warp_{w}x{h}_tinygrad.pkl").abspath]
lenv.Command(warp_targets, tinygrad_files + script_files, cmd)

def tg_compile(flags, model_name):
  pythonpath_string = 'PYTHONPATH="${PYTHONPATH}:' + env.Dir("#tinygrad_repo").abspath + '"'
  fn = File(f"models/{model_name}").abspath
  return lenv.Command(
    fn + "_tinygrad.pkl",
    [fn + ".onnx"] + tinygrad_files,
    f'{pythonpath_string} {flags} {image_flag} python3 {Dir("#tinygrad_repo").abspath}/examples/openpilot/compile3.py {fn}.onnx {fn}_tinygrad.pkl'
  )

# Compile small models
for model_name in ['dmonitoring_model']:
  tg_compile(tg_flags, model_name)

# Compile BIG model if USB GPU is available
if "USBGPU" in os.environ:
    print("USB GPU detected... building combined policy JIT")
    big_flags = "FLOAT16=1 JIT_BATCH_SIZE=0 IMAGE=0 USBGPU=1"
    script = File(Dir("#selfdrive/modeld").File("modeld.py").abspath)
    script_files = [script]
    big_policy_targets = []
    for cam in CAMERA_CONFIGS:
      w, h = cam.width, cam.height
      big_policy_targets.append(File(f"models/big_policy_{w}x{h}_tinygrad.pkl").abspath)
    # metadata + onnx models are inputs
    big_deps = [
      File("models/driving_vision_metadata.pkl").abspath,
      File("models/driving_policy_metadata.pkl").abspath,
      File("models/big_driving_vision.onnx").abspath,
      File("models/big_driving_policy.onnx").abspath,
    ] + warp_targets
    cmd = f'{big_flags} python3 {Dir("#selfdrive/modeld").abspath}/modeld.py --compile'
    lenv.Command(big_policy_targets, [script] + tinygrad_files + big_deps, cmd)
  else:
    print("USB GPU not detected... skipping")
